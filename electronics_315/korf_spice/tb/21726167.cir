* SPICE model for power amplifier
*-------------------------------------------------------------
* Author: Gerhardus Jozua Joubert Korf
* SU student number : 21726167
* Last mod: 10 May 2020
*=============================================================
* Description: This is a spice netlist for a class-AB amplifier which consists of a preamplifier and a power amplifier. This amplifier is within, or exceeds all specifications set out for it.
* specifications and tests for Electronics 315 practical 3
*=============================================================

*=============================================================
* Edit the "Evaluation_control" block below with your detail
* (do not remove the "*", which comments the line for NGSPICE, or "$" which is our control character).
* "Consent_to_publicity" (DEFAULT=FALSE) tells us to use your results on the "Hall of Fame" for Practical 3 - to be published on SUNLearn.
* This "Hall of Fame" will list the best results: highest signal power, lowest THD, lowest transistor temperature, etc.
* 
* "Nickname" (DEFAULT=YOURSTUDENTNUMBER) is the name/identifier to be used in the Hall of Fame. Profanity is not allowed. Delimit the text string
* with double quotation marks to allow spaces or special characters in the name.
*
* "Total_cost" (DEFAULT=12345.66) is the cost of your amplifier in Rand, as calculated from your complete component list for this circuit model.
*
*$BEGIN EVALUATION_CONTROL
*$CONSENT_TO_PUBLICITY=TRUE
*$NICKNAME="GJJKORF"
*$TOTAL_COST=199.70
*$END EVALUATION_CONTROL
*==============================================================

* YOUR CIRCUIT MUST INCLUDE:
* 1. A voltage source (DC 0) in series with the emitter of Qn, named Vicn, to measure I_{CQn}.
*    Its POSITIVE node must be connected to the emitter of Qn, and named "qne"
*      Example:  Vicn qne nodewhatever  DC  0
* 2. A voltage source (DC 0) in series with the emitter of Qp, named Vicp, to measure I_{CQp}.
*    Its NEGATIVE node must be connected to the emitter of Qp, and named "qpe"
*      Example:  Vicp nodewhateverelse  qpe  DC  0

* Include the transistor and diode models as an external file ("models.cir"); these are standard for the module
.include models.cir

* Define amplifier as a subcircuit to avoid node name clashes in test bench
* subckt name must be "audioamp"
* Pin sequence must be:  SignalInput+, SpeakerOut+, SpeakerOut-, SupplyVoltage+, SupplyVoltage-
.subckt audioamp in out outn vcc vee 

* Finally, your circuit is defined below:

* ============== CUT OUT BELOW AND BUILD YOUR CIRCUIT ====================

*Pre-Amplifier
Q7		3		1		4		t2N2219A
Q8		6		3		7		t2N2219A
Q9		8		7		9		t2N2219A

R6		2		1		3808805
R7		1		vee		158000
RC		5		3		300000
REP		4		vee		16501

RM		10		vee		1050

Cin		1		in 		470u

*VBE-Multiplier
Q6		16		13		12		t2N2219A
RS		12		21		4155
R1		14		13		2100
R2		13		12		700

CB		9		11		4.7u

*Current Source
Q5		15		17		19		t2N2905A
R3		vcc		18		8150
R4		17		0		39408
RE  	20		19		571

*Darlington Pairs
Q1		23		15		24		t2N2219A
Q2		22		24		qne		TIP41C
Q3		26		12		27		t2N2905A
Q4		25		27		qpe		TIP42C

Cout	28		out		2200u

*Current Measuremnt Voltage Sources
VIR67		vcc		2		DC		0		
VIRCEPQ7	vcc		5		DC		0
VIQ8		vcc		6		DC		0
VIQ9		vcc		8		DC		0
VIRM		9		10		DC		0
VIPRE		11		12		DC		0
VIR1		15		14		DC		0
VIQ6		15		16		DC		0
VIRS		21		vee 	DC		0
VIR3		18		17		DC		0
VIRE 		vcc		20		DC		0
VIQ1		vcc		23		DC		0
VIQ2		vcc		22		DC		0
VIQ3		26		vee		DC		0
VIQ4		25		vee		DC		0
vicn		qne		28		DC		0
vicp 		28		qpe		DC		0

* ============== CUT OUT ABOVE AND BUILD YOUR CIRCUIT ====================

* low resistance speaker connection to ground (to avoid putting "0" in the .subckt line

Rcable outn 0  1m

.ends
* ============================================


