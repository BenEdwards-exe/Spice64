ELECTRONICS PRACTICAL 3 SPICE AMPLIFIER CIRCUIT
.include models.cir

Vin 	in		0		sin		0		1		1k		20m
*Vin 	in 		0 		AC 		1 		sin(0 1 1k)
Vplus	vcc		0		DC		20
Vmin	vee		0		DC		-20
Rcable	outn	0		1m
Rload	out 	outn	6

*Pre-Amplifier
Q7		3		1		4		t2N2219A
Q8		6		3		7		t2N2219A
Q9		8		7		9		t2N2219A

R6		2		1		3808805
R7		1		vee		158000
RC		5		3		300000
REP		4		vee		16501

RM		10		vee		1050

Cin		29		in 		470u
VIX		29		1		DC 		0


*VBE-Multiplier
Q6		16		13		12		t2N2219A
RS		12		21		4155
R1		14		13		2100
R2		13		12		700

CB		9		11		4.7u

*Current Source
Q5		15		17		19		t2N2905A
R3		vcc		18		8150
R4		17		0		39408
RE  	20		19		571

*Power Stage
Q1		23		15		24		t2N2219A
Q2		22		24		qne		TIP41C
Q3		26		12		27		t2N2905A
Q4		25		27		qpe		TIP42C

Cout	28		out		2200u


*Current Measuremnt Voltage Sources
VIR67		vcc		2		DC		0		
VIRCEPQ7	vcc		5		DC		0
VIQ8		vcc		6		DC		0
VIQ9		vcc		8		DC		0
VIRM		9		10		DC		0
VIPRE		11		12		DC		0
VIR1		15		14		DC		0
VIQ6		15		16		DC		0
VIRS		21		vee 	DC		0
VIR3		18		17		DC		0
VIRE 		vcc		20		DC		0
VIQ1		vcc		23		DC		0
VIQ2		vcc		22		DC		0
VIQ3		26		vee		DC		0
VIQ4		25		vee		DC		0
vicn		qne		28		DC		0
vicp 		28		qpe		DC		0

*THD
.four 1k v(out)
*THD

.control

*FREQUENCY RESPONSE
*AC DEC 100 010 10meg
*plot mag(v(out)/v(in))
*plot mag(v(out)/v(9))
*plot mag(v(9)/v(in))

*FREQUENCY RESPONSE

*TRANSIENT RESPONSE
tran 1m 50m 0 1u

let powerQ1 = (v(23)-v(24))*i(VIQ1)
let powerQ2 = (v(22)-v(qne))*i(VIQ2)
let powerQ3 = (v(27)-v(26))*i(VIQ3)
let powerQ4 = (v(qpe)-v(25))*i(VIQ4)
let powerQ5 = (v(19)-v(15))*i(VIRE)
let powerQ6 = (v(16)-v(12))*i(VIQ6)
let powerQ7 = (v(3)-v(4))*i(VIRCEPQ7)
let powerQ8 = (v(6)-v(7))*i(VIQ8)
let powerQ9 = (v(8)-v(9))*i(VIQ9)

let powerR1  = (v(14)-v(13))*i(VIR1)
let powerR2  = (v(13)-v(12))*(v(13)-v(12))/700
let powerR3  = (v(vcc)-v(18))*i(VIR3)
let powerR4  = (v(17)*v(17))/39408
let powerR6  = (v(2)-v(1))*i(VIR67)
let powerR7  = (v(1)-v(vee))*i(VIR67)
let powerRC  = (v(5)-v(3))*i(VIRCEPQ7)
let powerREP = (v(4)-v(vee))*i(VIRCEPQ7)
let powerRM	 = (v(10)-v(vee))*i(VIRM)
let powerRE	 = (v(20)-v(19))*i(VIRE)
let powerRS	 = (v(12)-v(21))*i(VIRS)
let powerRL	 = (v(out)*v(out))/6.001

let powerQ1 = (v(23)-v(24))*i(VIQ1)
meas tran Power_q1_quiescent avg powerQ1 FROM=10m TO=20m
meas tran Power_q1_signal	 avg powerQ1 FROM=40m TO=50m
meas tran Power_q2_quiescent avg powerQ2 FROM=10m TO=20m
meas tran Power_q2_signal	 avg powerQ2 FROM=40m TO=50m
meas tran Power_q3_quiescent avg powerQ3 FROM=10m TO=20m
meas tran Power_q3_signal	 avg powerQ3 FROM=40m TO=50m
meas tran Power_q4_quiescent avg powerQ4 FROM=10m TO=20m
meas tran Power_q4_signal	 avg powerQ4 FROM=40m TO=50m
meas tran Power_q5_quiescent avg powerQ5 FROM=10m TO=20m
meas tran Power_q5_signal	 avg powerQ5 FROM=40m TO=50m
meas tran Power_q6_quiescent avg powerQ6 FROM=10m TO=20m
meas tran Power_q6_signal	 avg powerQ6 FROM=40m TO=50m
meas tran Power_q7_quiescent avg powerQ7 FROM=10m TO=20m
meas tran Power_q7_signal	 avg powerQ7 FROM=40m TO=50m
meas tran Power_q8_quiescent avg powerQ8 FROM=10m TO=20m
meas tran Power_q8_signal	 avg powerQ8 FROM=40m TO=50m
meas tran Power_q9_quiescent avg powerQ9 FROM=10m TO=20m
meas tran Power_q9_signal	 avg powerQ9 FROM=40m TO=50m

meas tran Power_r1_quiescent  avg powerR1 FROM=10m TO=20m
meas tran Power_r1_signal	  avg powerR1 FROM=40m TO=50m
meas tran Power_r2_quiescent  avg powerR2 FROM=10m TO=20m
meas tran Power_r2_signal	  avg powerR2 FROM=40m TO=50m
meas tran Power_r3_quiescent  avg powerR3 FROM=10m TO=20m
meas tran Power_r3_signal	  avg powerR3 FROM=40m TO=50m
meas tran Power_r4_quiescent  avg powerR4 FROM=10m TO=20m
meas tran Power_r4_signal	  avg powerR4 FROM=40m TO=50m
meas tran Power_r6_quiescent  avg powerR6 FROM=10m TO=20m
meas tran Power_r6_signal	  avg powerR6 FROM=40m TO=50m
meas tran Power_r7_quiescent  avg powerR7 FROM=10m TO=20m
meas tran Power_r7_signal	  avg powerR7 FROM=40m TO=50m
meas tran Power_rep_quiescent avg powerREP FROM=10m TO=20m
meas tran Power_rep_signal	  avg powerREP FROM=40m TO=50m
meas tran Power_rm_quiescent  avg powerRM FROM=10m TO=20m
meas tran Power_rm_signal	  avg powerRM FROM=40m TO=50m
meas tran Power_re_quiescent  avg powerRE FROM=10m TO=20m
meas tran Power_re_signal	  avg powerRE FROM=40m TO=50m
meas tran Power_rs_quiescent  avg powerRS FROM=10m TO=20m
meas tran Power_rs_signal	  avg powerRS FROM=40m TO=50m
meas tran Power_rl_quiescent  avg powerRL FROM=10m TO=20m
meas tran Power_rl_signal	  avg powerRL FROM=40m TO=50m
*TRANSIENT RESPONSE


run
.endc

.end


